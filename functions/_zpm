#compdef zpm
#autoload

autoload -Uz _ZPM_get_plugin_name

local _zpm_completions_plugins_local=()
for plugin in "$_ZPM_PLUGIN_DIR/"*; do
  _zpm_completions_plugins_local+=( ${${plugin:t}//---/\/} )
done

local _zpm_completions_plugins_loadable=($(
    echo ${_zpm_completions_plugins_local[@]} ${zsh_loaded_plugins[@]} | tr ' ' '\n' | sort | uniq -u
))

local _load_args=(
  'load:Load plugin'
  'if:Load plugin if condition true'
  'if-not:Load plugin if condition false'
)

local _zpm_all_args=(
  'load:Load plugin'
  'if:Load plugin if condition true'
  'if-not:Load plugin if condition false'
  'clean:Remove zpm cache'
  'upgrade:Upgrade plugin'
  $_zpm_extend_commands
)

local _zpm_completions_if_args=(
  'linux:Linux OS'
  'bsd:BSD OS'
  'macos:MacOS'
  'android:Android OS'
  'termux:Termux app'
  'ssh:SSH session'
  'vte:VTE based terminal emulator'
)

_arguments '*:: :->subcmds' && return 0

if (( CURRENT == 1 )); then
  _describe -t commands "zpm subcommand" _zpm_all_args
  return
fi

if [[ "$words[1]" == 'clean' ]]; then
  return
fi

if (( CURRENT > 1 )); then
  PRE=$((CURRENT - 1))
  PRE2=$((CURRENT - 2))
  if [[ "$words[$PRE]" == 'if' || "$words[$PRE]" == 'if-not' ]]; then
    _describe -t commands "zpm condition" _zpm_completions_if_args
  elif [[ "$words[$PRE2]" == 'if' || "$words[$PRE2]" == 'if-not'  ]]; then
    _describe -t commands "zpm next condition" _load_args
  elif [[ "$words[$PRE]" == 'load' ]]; then
    _describe -t commands "zpm load" _zpm_completions_plugins_loadable
  elif [[ "$words[$PRE]" == 'u' || "$words[$PRE]" == 'up' || "$words[$CURRENT]" == 'upgrade' ]]; then
    _describe -t commands "zpm upgrade plugins" zsh_loaded_plugins
  elif is-callable "_zpm_${words[$PRE]}_completion"; then
    "_zpm_${words[$PRE]}_completion"
  else
    _describe -t commands "zpm subcommand" zsh_loaded_plugins
    return
  fi
fi

return