#!/usr/bin/env zsh

declare -Ag namespaces=(
  empty empty
  omz omz
  gh github
  github github
  gl gitlab
  gitlab gitlab
  bb bitbucket
  bitbucket bitbucket
)

local Plugin_name="$1"

if [[ "$Plugin_name" == "@"* ]]; then

  if [[ "$Plugin_name" == '@zpm' ]]; then
    echo 'zpm'
    return 0
  fi

  if [[ "$Plugin_name" == '@omz' ]]; then
    echo 'omz-core'
    return 0
  fi

  for namespace_key namespace in ${(@kv)namespaces}; do
    if [[ "$Plugin_name" == "@${namespace_key}/"* || "$Plugin_name" == "@${namespace_key}" ]];then
      echo "$namespace"
      return 0
    fi
  done

  return -1
fi

local _ZPM_tag_str=${${1##*,type:}%%\,*}
if [[ "$_ZPM_tag_str" == "$Plugin_name" ]]; then
  echo 'github'
  return 0
fi

for namespace_key namespace in ${(@kv)namespaces}; do
  if [[ "$_ZPM_tag_str" == "${namespace_key}" ]];then
    echo "$namespace"
    return 0
  fi
done

return -1
