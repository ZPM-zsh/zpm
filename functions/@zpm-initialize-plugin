#!/usr/bin/env zsh

local Plugin="$1"
local Plugin_name="$2"
local Plugin_basename=$(@zpm-get-plugin-basename "$Plugin_name")
local Plugin_path=$(@zpm-get-plugin-path "$Plugin_name")

zsh_loaded_plugins+=("$Plugin_name")

local zpm_plugin_source=true
local zpm_plugin_async=false
local zpm_plugin_bin_path=true
local zpm_plugin_functions_path=true
local zpm_plugin_autoload=''
local zpm_plugin_autoload_all=false

if [[ -f "${Plugin_path}/plugin-options.zsh" ]]; then
  source "${Plugin_path}/plugin-options.zsh"
fi

@zpm-log zpm:init "Initialize '${Plugin_name}'"

if [[ "$Plugin"  == *",apply:"* ]]; then
  local _ZPM_tag_str=${Plugin##*,apply}
  _ZPM_tag_str=${_ZPM_tag_str%%\,*}

  if [[ "$_ZPM_tag_str" != *':source'* ]]; then
    zpm_plugin_source=false
  fi

  if [[ "$_ZPM_tag_str" != *':path'* ]]; then
    zpm_plugin_bin_path=false
  fi

  if [[ "$_ZPM_tag_str" != *':fpath'* ]]; then
    zpm_plugin_functions_path=false
  fi
fi

if [[ "$Plugin"  == *",async"* ]]; then
  zpm_plugin_async=true
fi

if [[ "$Plugin" == *",autoload-all"* ]]; then
  zpm_plugin_autoload_all=true
fi

# Fpath
local zpm_functions_path
if [[ "$zpm_plugin_functions_path"  == "true" ]]; then
  if [[ "$Plugin" == *",fpath:"* ]]; then
    local zpm_fpath=${${1##*,fpath:}%%,*}
    zpm_functions_path="${Plugin_path}/${zpm_fpath}"
  elif [[ -d "${Plugin_path}/functions" ]]; then
    zpm_functions_path="${Plugin_path}/functions"
  else
    for file in "${Plugin_path}/_"*(N); do
      zpm_functions_path="$Plugin_path"
      break;
    done
  fi

  if [[ -n "${zpm_functions_path}" ]]; then
    @zpm-log zpm:init:fpath "Add to \$fpath '${zpm_functions_path}'"
    @zpm-addfpath "${zpm_functions_path}"
  fi
fi

# Autoload
if [[ "$zpm_plugin_autoload_all" == "true" && -n "${zpm_functions_path}" ]]; then
  for autoload_file in "${zpm_functions_path}/"*; do
    if [[ "${autoload_file:t}" != "_"* && "${autoload_file:t}" != *".zwc" ]];then
      zpm_plugin_autoload="${zpm_plugin_autoload}:${autoload_file:t}"
    fi
  done
fi

if [[ "$Plugin"  == *",autoload:"* ]]; then
  local _ZPM_autoload_str=${Plugin##*,autoload}
  _ZPM_autoload_str=${_ZPM_autoload_str%%\,*}
  zpm_plugin_autoload="${zpm_plugin_autoload}${_ZPM_autoload_str}"
fi

if [[ -n "$zpm_plugin_autoload" ]]; then
  @zpm-log zpm:init:autoload "Autoload ${zpm_plugin_autoload}"
  @zpm-add-autoload "$zpm_plugin_autoload"
fi

# Bin path
if [[ "$zpm_plugin_bin_path" == "true" ]]; then
  local zpm_bin_path

  if [[ "$Plugin" == *",path:"* ]]; then
    local zpm_path=${${1##*,path:}%%,*}
    zpm_bin_path="${Plugin_path}/${zpm_path}"
  else
    zpm_bin_path="${Plugin_path}/bin"
  fi

  @zpm-log zpm:init:path "Add to \$PATH '${zpm_bin_path:a}'"
  @zpm-addpath "${zpm_bin_path:a}"
fi

# Source plugin
if [[ "$zpm_plugin_source" == "true" ]]; then
  local _ZPM_plugin_file_path

  if [[ "$Plugin"  == *",source:"* ]]; then
    _ZPM_plugin_file_path=$(
      @zpm-get-plugin-file-path "${Plugin_path}" "${Plugin_basename}" "${${1##*,source:}%%,*}"
    )
  else
    _ZPM_plugin_file_path=$(
      @zpm-get-plugin-file-path "${Plugin_path}" "${Plugin_basename}"
    )
  fi

  if [[ -n "$_ZPM_plugin_file_path" ]]; then
    if [[ "$zpm_plugin_async" == "true" ]]; then
      @zpm-log zpm:init:source "Source '${_ZPM_plugin_file_path}', async"
      @zpm-async-source "${Plugin_name}" "${_ZPM_plugin_file_path}"
    else
      @zpm-log zpm:init:source "Source '${_ZPM_plugin_file_path}', sync"
      @zpm-source "${Plugin_name}" "${_ZPM_plugin_file_path}"
    fi
  else
    @zpm-log zpm:init:source "Plugin file for ${Plugin_name} is missing"
    @zpm-no-source "${Plugin_name}"
  fi
else
  @zpm-no-source "${Plugin_name}"
fi
